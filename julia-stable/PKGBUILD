pkgname=julia-stable
pkgver=1.2.0
pkgrel=1
pkgdesc='The Julia Language: A fresh approach to technical computing.'
arch=('x86_64')
url='https://julialang.org/'
license=('MIT')

source=("${pkgname}-${pkgver}::git+https://github.com/JuliaLang/Julia"
        "Make.user")
sha256sums=('SKIP'
            '6850ab8894ba4938055db6d104b689fe33bac188dca45d72248c5abd4010bf60')

options=('!strip' 'staticlibs')

makedepends=('libatomic_ops' 'python' 'git' 'cmake>=3.4.3')
depends=('openlibm>=0.5.5' 'openblas-lapack>=0.3.3' 'suitesparse>=4.1'
         'arpack>=3.5.0' 'libgit2>=0.23' 'pcre2>=10.00' 'gmp>=5.0' 'mpfr>=4.0'
         'curl>=7.50' 'libssh2>=1.7' 'mbedtls>=2.2' 'libutf8proc>=2.1')

provides=('julia')
conflicts=('julia')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  git checkout -b install v${pkgver}
  cp ${srcdir}/Make.user .
  make prefix=/usr --jobs `nproc`
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} prefix=/usr install
  mv ${pkgdir}/usr/etc ${pkgdir}/etc
  rm ${pkgdir}/usr/share/icons/hicolor/icon-theme.cache
}
